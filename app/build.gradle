buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.realm:realm-gradle-plugin:2.3.1'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId 'org.ghost.android'
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1_02_04_00      // 2 digits each for major, minor, patch, build number
                                    // example: 2.0.0-beta1 => 2_00_00_00, 2.0.0 => 2_00_00_01
        versionName '1.2.4'

        // enable support for vector drawables on API < 21
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        testApplicationId 'org.ghost.android.test'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        animationsDisabled = true
    }
//    signingConfigs {
//        release {
//            storeFile(file(System.getenv('KEYSTORE') ?: '') ?: '')
//            storePassword(System.getenv('KEYSTORE_PASSWORD') ?: '')
//            keyAlias(System.getenv('KEY_ALIAS') ?: '')
//            keyPassword(System.getenv('KEY_PASSWORD') ?: '')
//        }
//    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            ext.enableCrashlytics = file('fabric.properties').exists()
        }
        release {
//            signingConfig signingConfigs.release
            minifyEnabled false   // FIXME disabled because login, revoke token strangely stopped working
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = true
        }
    }

    sourceSets {
        // add shared test code to both unit and instrumentation tests
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    lintOptions {
        // check for lint errors but don't abort builds
        // I had to disable it because we don't get translations for new strings immediately
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

// don't lint for every build (run it manually when you want it to run)
tasks.whenTaskAdded { task ->
    if (task.name == 'lint') {
        task.enabled = false
    }
}

// Fabric / Crashlytics keys are mandatory for release builds
task checkFabricPropertiesExists {
    doLast {
        if (!file('fabric.properties').exists()) {
            throw new FileNotFoundException('Missing fabric.properties file for Fabric / Crashlytics!')
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.dependsOn checkFabricPropertiesExists
    }
}

repositories {
    mavenCentral()
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"

    // reactive extensions (rx)
    implementation "io.reactivex.rxjava2:rxjava:2.1.4"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"

    // networking / communication / data storage
    implementation("com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion") {
        exclude module: "okhttp"
    }
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okhttpVersion"
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0"
    implementation "com.squareup:otto:1.3.8"

    // UI
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:cardview-v7:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:design:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:percent:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:customtabs:$rootProject.ext.supportLibVersion"
    implementation "org.ocpsoft.prettytime:prettytime:4.0.1.Final"
    // using a fork because https://github.com/tsengvn/typekit/issues/9#issuecomment-324478332
    implementation "com.github.katoro:typekit:1.0.1"

    // misc
    implementation "com.jakewharton:butterknife:$rootProject.ext.butterKnifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$rootProject.ext.butterKnifeVersion"
    implementation("com.crashlytics.sdk.android:crashlytics:2.6.2@aar") {
        transitive = true
    }
    implementation("com.crashlytics.sdk.android:answers:1.3.7@aar") {
        transitive = true
    }
    implementation "com.github.hotchemi:permissionsdispatcher:$rootProject.ext.permissionsDispatcherVersion"
    kapt "com.github.hotchemi:permissionsdispatcher-processor:$rootProject.ext.permissionsDispatcherVersion"
    implementation "com.github.slugify:slugify:2.1.9"

    // debug
    debugImplementation "nl.littlerobots.rxlint:rxlint:+"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.5.4"
    debugImplementation "com.jakewharton.scalpel:scalpel:1.1.2"
    debugImplementation "io.palaima.debugdrawer:debugdrawer:0.7.0"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-commons:$rootProject.ext.debugdrawerVersion"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-okhttp3:$rootProject.ext.debugdrawerVersion"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-picasso:$rootProject.ext.debugdrawerVersion"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-scalpel:$rootProject.ext.debugdrawerVersion"
    debugImplementation "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$rootProject.ext.stethoVersion"
    debugImplementation "com.uphyca:stetho_realm:2.0.0"

    // test
    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation "uk.co.datumedge:hamcrest-json:0.2"
    testImplementation "org.mockito:mockito-core:2.10.+"
    testImplementation "com.squareup.retrofit2:retrofit-mock:$rootProject.ext.retrofitVersion"
    testImplementation "org.json:json:20170516"    // don't depend on Android's JSONObject: http://stackoverflow.com/a/30759769/504611
    testImplementation "com.squareup.okhttp3:mockwebserver:$rootProject.ext.okhttpVersion"

    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.androidJunitRunnerVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.ext.androidJunitRunnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.ext.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
}
